dnl Process this file with autoconf to produce a configure script.
AC_REVISION([$Id: configure.in,v 1.4 1999/03/12 23:22:30 crosser Exp crosser $])dnl

dnl $Log: configure.in,v $
dnl Revision 1.4  1999/03/12 23:22:30  crosser
dnl check utime and timezone things
dnl
dnl Revision 1.3  1999/03/12 10:06:23  crosser
dnl fix bad checks
dnl
dnl Revision 1.2  1999/03/10 22:34:43  crosser
dnl check rename func
dnl
dnl Revision 1.1  1999/03/06 13:16:06  crosser
dnl Initial revision
dnl

AC_INIT(eph_iob.c)
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_LANG_C
AC_PROG_CC
if test "X$ac_cv_prog_gcc" = "Xyes"; then
	CFLAGS="$CFLAGS -Wall"
fi
AC_PROG_MAKE_SET
AC_SUBST(PROGS)dnl
AC_PATH_PROG(ECHO, echo, echo)dnl
AC_PATH_PROG(LN, ln, ln)dnl
AC_PATH_PROG(MV, mv, mv)dnl
AC_PATH_PROG(RM, rm, rm)dnl
AC_SUBST(LIBPROGS)dnl
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, ar)dnl
AC_PROG_LN_S

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h sys/select.h unistd.h)
AC_CHECK_HEADERS(sched.h termios.h utime.h sys/utime.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE
AC_CHECK_SIZEOF(short,2)
AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)

dnl define INT16 and INT32
AC_ARG_WITH(int16, [  --with-int16=TYPE       integer type that is 16 bits (autodetect)],
	use_int16=1)
if test "X$with_int16" != "Xno" && test "X$with_int16" != "Xyes" && test "X$with_int16" != "X"; then
	INT16=$with_int16
else
	if test "X$ac_cv_sizeof_short" = "X2"; then
		INT16=short
	else
		if test "X$ac_cv_sizeof_int" = "X2"; then
			INT16=int
		fi
	fi
fi
AC_ARG_WITH(int32, [  --with-int32=TYPE       integer type that is 32 bits (autodetect)],
	use_int32=1)
if test "X$with_int32" != "Xno" && test "X$with_int32" != "Xyes" && test "X$with_int32" != "X"; then
	INT32=$with_int32
else
	if test "X$ac_cv_sizeof_long" = "X4"; then
		INT32=long
	else
		if test "X$ac_cv_sizeof_int" = "X4"; then
			INT32=int
		fi
	fi
fi
: {$INT16=unknown}
: {$INT32=unknown}
AC_SUBST(INT16)dnl
AC_SUBST(INT32)dnl
AC_DEFINE_UNQUOTED(INT16,$INT16)dnl
AC_DEFINE_UNQUOTED(INT32,$INT32)dnl

dnl Checks for library functions.
AC_CHECK_FUNCS(rename mkdir select strerror strftime strspn)
AC_CHECK_FUNCS(uname cfmakeraw sched_setscheduler nanosleep)
AC_FUNC_VPRINTF

dnl check if all necessary functions are detected
if test $ac_cv_header_termios_h != yes; then
	AC_MSG_ERROR([Cannot work without termios!])
fi
if test $ac_cv_func_select != yes; then
	AC_MSG_ERROR([Cannot work without select() function!])
fi
if test $ac_cv_func_vprintf != yes; then
	AC_MSG_ERROR([Cannot work without v*printf() functions!])
fi
if test $INT16 = unknown; then
	AC_MSG_ERROR([Cannot determine 16bit integer type!])
fi
if test $INT32 = unknown; then
	AC_MSG_ERROR([Cannot determine 32bit integer type!])
fi

AC_OUTPUT(Makefile)
